plugins {
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

version = '0.0.1'

testing {
    suites {
        test {
            useJUnitJupiter('5.9.3')
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(19)
    }
    withSourcesJar()
}

tasks.named('jar') {
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version
        )
    }
}

publishing {
  repositories {
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/nathanrsxtn/java-csv"
      credentials {
        username = System.getenv("GITHUB_ACTOR")
        password = System.getenv("GITHUB_TOKEN")
      }
    }
    publications {
        gpr(MavenPublication) {
            groupId = 'dev.nathanrsxtn.data'
            artifactId = 'csv'
            pom {
                name = 'Java CSV'
                description = 'Fast, zero-dependency, declarative CSV file reader for Java. '
                url = 'https://github.com/nathanrsxtn/java-csv'
                developers {
                    developer {
                        id = 'nathanrsxtn'
                        name = 'Nathan Sexton'
                        email = 'developer@nathanrsxtn.dev'
                    }
                }
            }
            from(components.java)
        }
    }
  }
}